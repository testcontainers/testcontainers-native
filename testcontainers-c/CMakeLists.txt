set(SRCS testcontainers-c.go)

set(SHIM_TARGET testcontainers-c-shim)
set(SHIM_TARGET_LIB testcontainers-c.so)
set(SHIM_TARGET_HEADER testcontainers-c.h)

add_custom_command(OUTPUT ${SHIM_TARGET_LIB} ${SHIM_TARGET_HEADER}
  DEPENDS ${SRCS}
  WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
  COMMAND env GOPATH=${GOPATH} go build -buildmode=c-shared
  -o "${CMAKE_CURRENT_BINARY_DIR}/${SHIM_TARGET_LIB}"
  ${CMAKE_GO_FLAGS} ${SRCS}
  COMMENT "Building Testcontainers C shared library")

add_custom_target(${SHIM_TARGET} DEPENDS ${SHIM_TARGET_LIB} ${SHIM_TARGET_HEADER})

set(TARGET testcontainers-c)
set(TARGET_NAME ${TARGET})
set(TARGET_DESCRIPTION ${PROJECT_DESCRIPTION})
set(TARGET_VERSION ${PROJECT_VERSION})

add_library(${TARGET} SHARED IMPORTED GLOBAL)
add_dependencies(${TARGET} ${SHIM_TARGET})
set_target_properties(${TARGET} PROPERTIES
    LINKER_LANGUAGE C
    VERSION ${TARGET_VERSION}
    PUBLIC_HEADER testcontainers-c.h
    IMPORTED_LOCATION ${CMAKE_CURRENT_BINARY_DIR}/${SHIM_TARGET_LIB}
    INTERFACE_INCLUDE_DIRECTORIES ${CMAKE_CURRENT_BINARY_DIR})

configure_file(cmake.pc.in ${TARGET}.pc @ONLY)
install(FILES ${SHIM_TARGET_LIB}
    DESTINATION ${CMAKE_INSTALL_LIBDIR})
install(FILES ${SHIM_TARGET_HEADER}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})
install(FILES ${CMAKE_BINARY_DIR}/${TARGET}.pc DESTINATION ${CMAKE_INSTALL_DATAROOTDIR}/pkgconfig)
